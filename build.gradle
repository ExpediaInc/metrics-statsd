buildscript {
  repositories {
    maven { url 'http://repo.springsource.org/plugins-release' }
  }
  dependencies {
    classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
  }
}

defaultTasks 'build'

subprojects {

  plugins.with {
    apply 'java'
    apply 'maven-publish'
    apply 'checkstyle'
    apply 'findbugs'
    apply 'jacoco'

    apply 'propdeps'
    apply 'propdeps-maven'
    apply 'propdeps-idea'
    apply 'propdeps-eclipse'
    apply 'com.jfrog.bintray'
  }

  group = 'com.readytalk'
  description = 'Metrics StatsD Support'

  repositories {
    mavenCentral()
  }

  dependencies {
    compile (
      'org.slf4j:slf4j-api:1.7.5',
    )

    optional (
      'com.google.code.findbugs:jsr305:2.0.1',
      'com.google.code.findbugs:annotations:2.0.1',
    )

    testCompile (
      'junit:junit:4.11',
      'org.mockito:mockito-all:1.9.5',
      'org.slf4j:slf4j-jdk14:1.7.5',
      'org.easytesting:fest-assert-core:2.0M10',
    )
  }

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  checkstyle {
    toolVersion = '5.6'
    showViolations = false
    sourceSets = [sourceSets.main]
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
  }

  findbugs {
    toolVersion = System.getProperty('java.version').startsWith('1.6') ? '2.0.1' : '3.0.1'
    sourceSets = [sourceSets.main]
    ignoreFailures = true
  }

  jacocoTestReport {
    reports {
      html.enabled = true
      csv.enabled = true
      xml.enabled = true
    }
  }

  publishing {
    publications {
      maven(MavenPublication) {
        from components.java

        version System.env['BUILD_NUMBER'] ?: '1.0.0-SNAPSHOT'

        artifact sourceJar {
          classifier "sources"
        }

        pom.withXml {
          asNode().dependencies.'*'.findAll() {
            it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
              dep.name == it.artifactId.text()
            }
          }.each() {
            it.scope*.value = 'compile'
          }
        }
      }
    }

    repositories {
      maven {
        credentials {
          username System.env['USERNAME'] ?: ''
          password System.env['PASSWORD'] ?: ''
        }

        name = 'tls-artifactory'
        url = 'https://artylab.expedia.biz/tls-maven-release-local'
      }
    }
  }

  bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? bintrayKey : System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
      userOrg = 'readytalk'
      repo = 'maven'
      name = 'metrics-statsd'
    }
  }

  artifacts {
    archives sourceJar
  }

  jacocoTestReport.dependsOn test
  build.dependsOn javadoc, jacocoTestReport
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.8'
}
